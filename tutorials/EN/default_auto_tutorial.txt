## Please note that all actions from all_tutorials_preamble.txt will be added here!

c:html.ItemState home-start 2
c:showQ a True
c:showQ b False

p:Welcome to this introductory <font color="rgb(255,100,50)">Olex2</font color="rgb(255,100,50)"> tutorial. Press 'Next' to advance to the next screen in the tutorial or 'Cancel' to leave the tutorial.<br><br>Structures are loaded from the <b>Start</b> tool tab, which will be highlighted when you press 'Next'.
#c:@py "import time"
#c:@py "time.sleep(5)"
h:h2-Start
c:html.ItemState home-start 1

p:The files needed for solving and refining a crystal structure with Olex2 are a reflections file (<b>name.hkl</b>) and an instructions file (<b>name.ins</b>) containing information about the unit cell, space group, radiation type, etc. These files should have the same name and reside in the same folder, normally accessed with the <b>Open</b> button.

p:Olex2 comes with several sample structures, which are shown as buttons in this tool tab. The <b>Sucrose</b> structure will be solved and refined in this tutorial. Press 'Next' to load it.
c:refresh
c:freeze(true)
c:reap DataDir()/samples/sucrose/sucrose.res
c:spy.revert_to_original()
c:spy.gui.get_Z_prime_from_fraction(1)
c:html.Update
c:SetVar(solution_output, strcat(FileName(),_solution_output.html))
c:reset
c:freeze(false)
c:refresh

p:To access all the main solution and refinement tools in Olex2, click the <b>Work</b> tab.
h:tab-work;3
c:html.ItemState * 0 tab* 2 tab-work 1 logo1 1 index-work* 1 info-title 1

p:From this tab, one can <b>Solve</b>, <b>Refine</b>, <b>Draw</b> and produce a final <b>Report</b> for a structure. This tutorial is mainly about the first two of these functions.
h:btn-solve;1
h:btn-refine;1
h:btn-draw;1
h:btn-report;1

p:Click on the arrow next to the <b>Solve</b> button to set the solve options.
h:cbtn-solve
c:html.ItemState cbtn* 1 cbtn-solve 2 *settings 0 solve-settings 1

p:Here we can choose solution programs (e.g., ShelXT, olex.solve) and other solve settings...
h:SET_snum_solution_PROGRAM_bg
h:SET_snum_solution_METHOD_bg

p:...set the <b>Formula</b> and can determine the <b>space group</b>.
h:SET_SNUM_REFINEMENT_FORMULA_bg
h:SET_SNUM_REFINEMENT_SPACE_GROUP_bg

p:When you click on <b>Suggest SG</b> link, or type '<c>sg</c>', watch what happens in the graphics window!
c:sg
c:refresh

p:In the Solve tool tab area, choose a solution program from the <b>Program</b> drop-down menu, make sure <b>name.hkl</b> is loaded as the <b>Reflections</b> file, and enter the expected molecular formula in <b>Composition</b>.
c:spy.set_solution_program(olex2.solve,,snum)
c:html.Update
c:SetVar(solution_output, strcat(FileName(),_solution_output.html))

p:Click the <b>Solve</b> button. Olex2 has many line commands that are entered at the command prompt in the graphics window. In this instance, instead of clicking the <b>Solve</b> button, one could type in '<c>solve</c>' instead.
h:btn-solve;1
c:solve
c:waitfor process
c:refresh

p:Every line command has a short help entry describing its function and listing any switches that the command may take. Type '<c>help clean</c>' to see the entry for the '<c>clean</c>' command used throughout this tutorial.
c:lines 15
c:help clean
c:refresh

p:The gold spheres that appear after solution are regions of high electron density (i.e., likely positions of atoms) and are called 'Q peaks'. Tidy up this solution by clicking the <b>Clean</b> button or by typing '<c>clean</c>' at the command prompt.
h:toolbar-tidy
c:clean
c:refresh

p:It is often helpful at this stage, especially if molecular fragments appear scattered across the screen, to compact them together in the center. To do this, click the <b>Center</b> button or type '<c>compaq -a</c>'.
h:toolbar-center
c:compaq -a
c:legend -r
c:refresh

p:A recognizable skeleton structure of sucrose is displayed on the screen, but all the atoms are carbon! This structure now needs to be 'refined' (correct atoms and atomic positions need to be assigned in the model). To start this important process, click the <b>Refine</b> button. Alternatively, type '<c>refine</c>' at the command prompt.
h:btn-refine;1
c:html.ItemState cbtn* 1 cbtn-refine 2 *settings 0 refine-settings 1
c:SetVar(refinement_output, strcat(FileName(),_refinement_output.html))
c:refine 4 4

p:Tidy up this refined structure again by clicking <b>Clean</b> or typing '<c>clean</c>' at the command prompt, as before.
c:clean

p:To identify all the non-hydrogen atoms correctly, the <b>Uiso Slider</b> in the <b>Toolbox Work</b> tool tab is first used to select carbons in the skeleton that look relatively small (these are likely to be oxygens).
c:html.ItemState work-main-toolbar 1
c:html.ItemState h3-peak-and-uiso-sliders 1
c:html.setBG(UISO_SELECT_SLIDER,#ff0000)
c:html.setvalue(UISO_SELECT_SLIDER,-7)
c:html.setvalue(UisoSelectVal,<0.035)
c:sel atoms where xatom.uiso < 0.035&&xatom.type!='Q'
c:refresh

p:Convert the selected atoms to oxygens by clicking the <b>O</b> button.
#c:spy.ElementButtonStates(O)
c:name sel O
c:sel -u
c:refresh

p:The Uiso slider method is a coarse technique, and can miss some atom assignments. Select the carbon that was not identified as an oxygen by clicking on it, and convert it to oxygen as well.
c:name C10 O
c:refresh

p:We now refine this updated structure containing the newly identified oxygens.
c:refine 4 4

p:Click the <b>Clean</b> button after the refinement, as before.
c:clean
c:refresh

p:These non-hydrogen atoms are spherical. To make them anisotropic, click on the button showing the anisotropic atom in the <b>Toolbox Work</b> tool tab, or just type '<c>anis</c>'.
h:toolbar-anis
c:anis
c:refine

p:Click the <b>Clean</b> button after this refinement once again.
c:clean
c:refresh

p:The hydrogen atoms will finally be added as appropriate (in 'riding positions'). Click on the <b>Add H</b> button in the <b>Toolbox Work</b> tool tab or type '<c>hadd</c>' and refine again.
#h:small-Add_H
c:hadd
c:refine
c:refresh

p:You can toggle the display of the Q peaks, which now represent regions of residual electron density in the model, by repeatedly clicking the <b>Q peaks</b> button. The equivalent line command is either '<c>Q</c>' or '<c>CTRL+Q</c>'.
h:toolbar-Q
c:showQ a False
c:refresh

p:In order to see all the console output in the graphics window, you can type '<c>lines -1</c>'.
c:lines -1
c:refresh

p:To show only 10 lines, type '<c>lines 10</c>'.
c:lines 10
c:refresh
c:html.ItemState solve-settings 2

p:If you type '<c>text</c>' or press the little text icon, you will get a 'transcript' of the console in your text editor.
h:toolbar-text
c:text
c:refresh

p:To clear the console output, type '<c>clear</c>'.
c:clear
c:refresh
c:html.ItemState solve-settings 2

p:We hope you enjoyed this first introduction to Olex2 - please let us know if we missed out any essential steps in this manual by visiting www.olex2.org and getting engaged in the Olex2 development.
c:spy.demo.switch_tab_for_tutorials(home)

p:End of tutorial, press 'Next' to repeat the tutorial or 'Cancel' to exit. <br><font size='1'>Written by Stefanie Freitag-Pohl.</font>